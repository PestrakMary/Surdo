import org.gradle.work.Incremental

import java.nio.file.Paths

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "by.surdoteam.surdo"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 3
        versionName "0.4.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':pocketsphinx-android-5prealpha-release')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.fragment:fragment:1.3.4"
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
}

abstract class BuildGramTask extends DefaultTask {
    @Incremental
    @InputFile
    abstract RegularFileProperty getLib();

    @Incremental
    @InputFile
    abstract RegularFileProperty getDict();

    @OutputFile
    abstract RegularFileProperty getGramLong();

    @OutputFile
    abstract RegularFileProperty getGram();

    @TaskAction
    void execute() {
        println 'buildGram is in building'
        def libCommands = lib.get().asFile.collect() { line ->
            line.split(", ", 2)[0]
        }
        def dictWords = dict.get().asFile.collect() { line ->
            line.split(" ", 2)[0]
        }
        libCommands.each { command ->
            command.split(' ').each { word ->
                if (!dictWords.contains(word)) {
                    throw new Exception("car.dict does not contain word ${word}, but lib.txt does")
                }
            }
        }
        gramLong.get().asFile.write("""#JSGF V1.0;

grammar car;

<item> = (${libCommands.join(") | (")});

public <command> = ( <item> ) + ;
""",'utf-8')
        gram.get().asFile.write("""#JSGF V1.0;

grammar car;

<item> = (${libCommands.join(") | (")});

public <command> = ( <item> );
""",'utf-8')
    }
}

task buildGram(type: BuildGramTask) {
    def assetsDir = android.sourceSets.main.assets.srcDirs[0].toString()
    lib = file(Paths.get(android.sourceSets.main.res.srcDirs[0].toString(), 'raw', 'lib.txt'))
    dict = file(Paths.get(assetsDir, 'sync', 'car.dict').toString())
    gramLong = file(Paths.get(assetsDir, 'sync', 'car_long.gram').toString())
    gram = file(Paths.get(assetsDir, 'sync', 'car.gram').toString())
}

ant.importBuild 'assets.xml'
list.dependsOn(buildGram)
checksum.dependsOn(buildGram)
preBuild.dependsOn(list, checksum)
clean.dependsOn(clean_assets)